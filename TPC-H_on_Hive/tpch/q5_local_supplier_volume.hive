set mapreduce.job.queuename=tpch;

DROP TABLE IF EXISTS q5_customer;
DROP TABLE IF EXISTS q5_orders;
DROP TABLE IF EXISTS q5_lineitem;
DROP TABLE IF EXISTS q5_supplier;
DROP TABLE IF EXISTS q5_nation;
DROP TABLE IF EXISTS q5_region;
DROP TABLE IF EXISTS q5_local_supplier_volume;

-- create tables and load data
-- ROW FORMAT SERDE
--   'org.apache.hadoop.hive.serde2.columnar.ColumnarSerDe'
-- STORED AS INPUTFORMAT
--   'org.apache.hadoop.hive.ql.io.RCFileInputFormat'
-- OUTPUTFORMAT
--   'org.apache.hadoop.hive.ql.io.RCFileOutputFormat'

-- create external table customer (C_CUSTKEY INT, C_NAME STRING, C_ADDRESS STRING, C_NATIONKEY INT, C_PHONE STRING, C_ACCTBAL DOUBLE, C_MKTSEGMENT STRING, C_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE LOCATION '/tpch/customer';
CREATE EXTERNAL TABLE IF NOT EXISTS q5_customer (C_CUSTKEY INT, C_NAME STRING, C_ADDRESS STRING, C_NATIONKEY INT, C_PHONE STRING, C_ACCTBAL DOUBLE, C_MKTSEGMENT STRING, C_COMMENT STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE
LOCATION '/tpch/customer';

-- Create external table lineitem (L_ORDERKEY INT, L_PARTKEY INT, L_SUPPKEY INT, L_LINENUMBER INT, L_QUANTITY DOUBLE, L_EXTENDEDPRICE DOUBLE, L_DISCOUNT DOUBLE, L_TAX DOUBLE, L_RETURNFLAG STRING, L_LINESTATUS STRING, L_SHIPDATE STRING, L_COMMITDATE STRING, L_RECEIPTDATE STRING, L_SHIPINSTRUCT STRING, L_SHIPMODE STRING, L_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE LOCATION '/tpch/lineitem';
cREATE EXTERNAL TABLE IF NOT EXISTS q5_lineitem (L_ORDERKEY INT, L_PARTKEY INT, L_SUPPKEY INT, L_LINENUMBER INT, L_QUANTITY DOUBLE, L_EXTENDEDPRICE DOUBLE, L_DISCOUNT DOUBLE, L_TAX DOUBLE, L_RETURNFLAG STRING, L_LINESTATUS STRING, L_SHIPDATE STRING, L_COMMITDATE STRING, L_RECEIPTDATE STRING, L_SHIPINSTRUCT STRING, L_SHIPMODE STRING, L_COMMENT STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE
LOCATION '/tpch/lineitem';

-- create external table orders (O_ORDERKEY INT, O_CUSTKEY INT, O_ORDERSTATUS STRING, O_TOTALPRICE DOUBLE, O_ORDERDATE STRING, O_ORDERPRIORITY STRING, O_CLERK STRING, O_SHIPPRIORITY INT, O_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE LOCATION '/tpch/orders';
CREATE EXTERNAL TABLE IF NOT EXISTS q5_orders (O_ORDERKEY INT, O_CUSTKEY INT, O_ORDERSTATUS STRING, O_TOTALPRICE DOUBLE, O_ORDERDATE STRING, O_ORDERPRIORITY STRING, O_CLERK STRING, O_SHIPPRIORITY INT, O_COMMENT STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE
LOCATION '/tpch/orders';

-- create external table supplier (S_SUPPKEY INT, S_NAME STRING, S_ADDRESS STRING, S_NATIONKEY INT, S_PHONE STRING, S_ACCTBAL DOUBLE, S_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE LOCATION '/tpch/supplier';
CREATE EXTERNAL TABLE IF NOT EXISTS q5_supplier (S_SUPPKEY INT, S_NAME STRING, S_ADDRESS STRING, S_NATIONKEY INT, S_PHONE STRING, S_ACCTBAL DOUBLE, S_COMMENT STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE
LOCATION '/tpch/supplier';

-- create external table nation (N_NATIONKEY INT, N_NAME STRING, N_REGIONKEY INT, N_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE LOCATION '/tpch/nation';
CREATE EXTERNAL TABLE IF NOT EXISTS q5_nation (N_NATIONKEY INT, N_NAME STRING, N_REGIONKEY INT, N_COMMENT STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE
LOCATION '/tpch/nation';

-- create external table region (R_REGIONKEY INT, R_NAME STRING, R_COMMENT STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE LOCATION '/tpch/region';
CREATE EXTERNAL TABLE IF NOT EXISTS q5_region (R_REGIONKEY INT, R_NAME STRING, R_COMMENT STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '|' STORED AS TEXTFILE
LOCATION '/tpch/region';

-- create the target table
CREATE TABLE IF NOT EXISTS q5_local_supplier_volume (N_NAME STRING, REVENUE DOUBLE);

set mapred.min.split.size=536870912;
set hive.mapred.reduce.tasks.speculative.execution=true;

-- the query
insert overwrite table q5_local_supplier_volume 
select 
  n_name, sum(l_extendedprice * (1 - l_discount)) as revenue 
from
  q5_customer c join
    ( select n_name, l_extendedprice, l_discount, s_nationkey, o_custkey from q5_orders o join
      ( select n_name, l_extendedprice, l_discount, l_orderkey, s_nationkey from q5_lineitem l join
        ( select n_name, s_suppkey, s_nationkey from q5_supplier s join
          ( select n_name, n_nationkey 
            from q5_nation n join q5_region r 
            on n.n_regionkey = r.r_regionkey and r.r_name = 'ASIA'
          ) n1 on s.s_nationkey = n1.n_nationkey
        ) s1 on l.l_suppkey = s1.s_suppkey
      ) l1 on l1.l_orderkey = o.o_orderkey and o.o_orderdate >= '1994-01-01' 
              and o.o_orderdate < '1995-01-01'
) o1 
on c.c_nationkey = o1.s_nationkey and c.c_custkey = o1.o_custkey
group by n_name 
order by revenue desc;

